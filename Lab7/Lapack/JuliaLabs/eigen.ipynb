{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "fb833b50",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Матрица A из файла:\n",
      "4×4 Matrix{Float64}:\n",
      " 260.39  167.32  230.97  134.64\n",
      " 167.32  125.43  146.51   66.11\n",
      " 230.97  146.51  211.38  123.79\n",
      " 134.64   66.11  123.79  100.94\n",
      "\n",
      "[1.8405567906769327, 3.9113661266833457, 46.23723138184363, 646.1508457008049]\n"
     ]
    }
   ],
   "source": [
    "using LinearAlgebra\n",
    "\n",
    "function read_matrix(path::AbstractString)\n",
    "    lines = readlines(path)\n",
    "    rows = [parse.(Float64, split(line)) for line in lines if !isempty(strip(line))]\n",
    "    n = length(rows)\n",
    "    m = length(rows[1])\n",
    "    @assert n == m \"Матрица должна быть квадратной\"\n",
    "    A = Matrix{Float64}(undef, n, n)\n",
    "    for i in 1:n, j in 1:n\n",
    "        A[i, j] = rows[i][j]\n",
    "    end\n",
    "    return A\n",
    "end\n",
    "\n",
    "function danilevsky_algorithm(A::Matrix{Float64})\n",
    "    n = size(A, 1)\n",
    "    B = Matrix{Float64}(I, n, n)\n",
    "    P = copy(A)\n",
    "\n",
    "    for k in n:-1:2\n",
    "        B_k = Matrix{Float64}(I, n, n)\n",
    "        for j in 1:n\n",
    "            if j != k - 1\n",
    "                B_k[k - 1, j] = -P[k, j] / P[k, k - 1]\n",
    "            else\n",
    "                B_k[k - 1, j] = 1.0 / P[k, k - 1]\n",
    "            end\n",
    "        end\n",
    "\n",
    "        B_k_inverted = Matrix{Float64}(I, n, n)\n",
    "        for j in 1:n\n",
    "            B_k_inverted[k - 1, j] = P[k, j]\n",
    "        end\n",
    "\n",
    "        P = B_k_inverted * P * B_k\n",
    "        B = B * B_k\n",
    "    end\n",
    "\n",
    "    return B, P\n",
    "end\n",
    "\n",
    "function union_intervals(intervals::Vector{Vector{Float64}})\n",
    "    isempty(intervals) && return Vector{Vector{Float64}}()\n",
    "    s = sort(intervals, by = x -> x[1])\n",
    "    merged = [copy(s[1])]\n",
    "    for v in s[2:end]\n",
    "        last = merged[end]\n",
    "        if last[2] >= v[1]\n",
    "            last[2] = max(last[2], v[2])\n",
    "        else\n",
    "            push!(merged, copy(v))\n",
    "        end\n",
    "    end\n",
    "    merged\n",
    "end\n",
    "\n",
    "function gershgorin_intervals_raw(A::Matrix{Float64})\n",
    "    n = size(A, 1)\n",
    "    ivs = Vector{Vector{Float64}}(undef, n)\n",
    "    for i in 1:n\n",
    "        r = sum(abs.(A[i, :])) - abs(A[i, i])\n",
    "        c = A[i, i]\n",
    "        ivs[i] = [c - r, c + r]\n",
    "    end\n",
    "    ivs\n",
    "end\n",
    "\n",
    "gershgorin_intervals(A::Matrix{Float64}) = union_intervals(gershgorin_intervals_raw(A))\n",
    "\n",
    "find_equation_coeffs(D::Matrix{Float64}) = vcat(1.0, -D[1, :])\n",
    "\n",
    "function poly_eval(coeffs::AbstractVector{<:Real}, x::Real)\n",
    "    acc = 0.0\n",
    "    for c in coeffs\n",
    "        acc = acc * x + c\n",
    "    end\n",
    "    acc\n",
    "end\n",
    "\n",
    "function find_real_roots_on_intervals(coeffs::Vector{Float64},\n",
    "                                      intervals::Vector{Vector{Float64}};\n",
    "                                      step::Real = 1e-2, tol::Real = 1e-8)\n",
    "    roots = Float64[]\n",
    "    for I in intervals\n",
    "        a, b = I\n",
    "        x = a\n",
    "        prev_y = poly_eval(coeffs, x)\n",
    "        while x < b - step / 2\n",
    "            x_next = min(x + step, b)\n",
    "            y_next = poly_eval(coeffs, x_next)\n",
    "            if prev_y == 0.0\n",
    "                push!(roots, x)\n",
    "            elseif y_next == 0.0\n",
    "                push!(roots, x_next)\n",
    "            elseif prev_y * y_next < 0\n",
    "                left, right = x, x_next\n",
    "                yl = prev_y\n",
    "                while right - left > tol\n",
    "                    mid = (left + right) / 2\n",
    "                    ym = poly_eval(coeffs, mid)\n",
    "                    if yl * ym <= 0\n",
    "                        right = mid\n",
    "                    else\n",
    "                        left = mid\n",
    "                        yl = ym\n",
    "                    end\n",
    "                end\n",
    "                push!(roots, (left + right) / 2)\n",
    "            end\n",
    "            x = x_next\n",
    "            prev_y = y_next\n",
    "        end\n",
    "    end\n",
    "    sort!(unique!(round.(roots; digits = 10)))\n",
    "    roots\n",
    "end\n",
    "\n",
    "function danilevsky_eigenvalues(A::Matrix{Float64};\n",
    "                                step::Real = 1e-3,\n",
    "                                tol::Real = 1e-10)\n",
    "    _, Df = danilevsky_algorithm(A)\n",
    "    coeffs = find_equation_coeffs(Df)\n",
    "    intervals = gershgorin_intervals(A)\n",
    "    eig_vals = find_real_roots_on_intervals(coeffs, intervals; step = step, tol = tol)\n",
    "    return eig_vals\n",
    "end\n",
    "\n",
    "function main()\n",
    "    filename = \"matrix.txt\"\n",
    "    A = read_matrix(filename)\n",
    "    println(\"Матрица A из файла:\")\n",
    "    show(stdout, \"text/plain\", A)\n",
    "    println(\"\\n\")\n",
    "    eig_vals = danilevsky_eigenvalues(A)\n",
    "    println(eig_vals)\n",
    "end\n",
    "\n",
    "main()"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.2",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
